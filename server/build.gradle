// Server side build.gradle
// Builds the server side of the project

plugins {
  // Apply the application plugin to add support for building a CLI application
  id 'application'

  // Apply the Jacoco plugin to add suppport for JUnit test coverage reports.
  id 'jacoco'

  // Apply the checkstyle plugin to check for appropriate Java code style.
  id 'checkstyle'
}

// Build and run the project with Java 17
java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

// In this section you declare where to find the dependencies of your project
repositories {
  // Use Maven Central for resolving your dependencies.
  // You can declare any Maven/Ivy/file repository here.
  mavenCentral()
}

// The class where our server is located
mainClassName = 'umm3601.Server'

// External dependencies that our application utilizes
dependencies {

  // Javalin, a simple web framework for Java
  implementation 'io.javalin:javalin:6.1.2'

  // Jackson databaing library for JSON
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'

  // Simple Logging Facade for Java
  implementation 'org.slf4j:slf4j-simple:2.0.12'

  // JUnit Jupiter API for testing.
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'

  // JUnit Jupiter Engine for testing.
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

  // Mockito for testing
  testImplementation 'org.mockito:mockito-core:5.10.0'
}

application {
  // Define the main class for the application
  mainClass = 'umm3601.Main'
}

test {
  // Use junit platform for unit tests
  useJUnitPlatform()

  // After running the tests, generate a coverage report
  finalizedBy jacocoTestReport

  // After running the tests, check the coverage level
  finalizedBy jacocoTestCoverageVerification
}

jacocoTestReport {
    // Running the test report task automatically runs test first
  dependsOn test

  reports {
    // This isn't strictly necessary, but the default reports
    // location is buried pretty deep in the build directory,
    // so this makes it easier to find.
    html.destination file("${buildDir}/jacocoHtml")
  }

  afterEvaluate {
    // This excludes the `Server` class from the coverage report. We don't
    // have any good way to test the `Server` class directory (we'd have
    // to somehow fake incoming HTTP requests), so we are just
    // leaving it out of the coverage report and the coverage limits.
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ['umm3601/Main.class', 'umm3601/Server.class'])
    }))
  }
}

tasks.withType(JavaCompile) {
  // All of our source files are written in UTF-8.
  options.encoding = 'UTF-8'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
